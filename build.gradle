import net.minecraftforge.forge.tasks.*
import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'net.minecraftforge.gradleutils' version '[2.2,2.3)'
    id 'eclipse'
    id 'de.undercouch.download' version '5.4.0'
    id 'net.minecraftforge.gradle.patcher' version '[6.0.16,6.2)' apply false
    id 'net.minecraftforge.gradle.mcp' version '[6.0.16,6.2)' apply false
    id 'net.minecraftforge.gradlejarsigner' version '1.0.4'
    id 'org.barfuin.gradle.taskinfo' version '2.1.0'
}

Util.init() //Init all our extension methods!

ext {
    GIT_INFO = gradleutils.gitInfo
    VERSION = gradleutils.getFilteredMCTagOffsetBranchVersion(true, '[0-9]', MC_VERSION)
    FORGE_VERSION = VERSION.substring(MC_VERSION.length() + 1)

    BINPATCH_TOOL = 'net.minecraftforge:binarypatcher:1.2.0:fatjar'
    INSTALLER_TOOLS = 'net.minecraftforge:installertools:1.4.0'
    FART = 'net.minecraftforge:ForgeAutoRenamingTool:1.0.6'
}

changelog {
    fromTag '47.999'
}

tasks.register('setup') {
    dependsOn ':forge:extractMapped'
    if (findProject(':clean'))
        dependsOn ':clean:extractMapped'
}

tasks.register('doChecks') {
    dependsOn ':forge:checkJarCompatibility'
    dependsOn ':forge:publish'
}

project(':mcp') {
    apply plugin: 'net.minecraftforge.gradle.mcp'
    mcp {
        config MC_VERSION + '-' + MCP_VERSION
        pipeline = 'joined'
    }
}

if (System.env.TEAMCITY_VERSION) {
    //Only setup the CI environment if and only if the environment variables are set.
    tasks.configureTeamCity {
        doLast {
            println "##teamcity[buildNumber '${project(':forge').version}']"
            println "##teamcity[setParameter name='env.PUBLISHED_JAVA_ARTIFACT_VERSION' value='${project(':forge').version}']"
        }
    }
}