import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.cadixdev.licenser'
    id 'net.minecraftforge.gradleutils'
}

apply from: rootProject.file('build_shared.gradle')

dependencies {
    compileOnly(libs.nulls)
    implementation(project(':fmlloader'))
    implementation(project(':fmlcore'))
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            'Automatic-Module-Name': 'net.minecraftforge.javafmlmod',
            'FMLModType': 'LANGPROVIDER'
        ] as LinkedHashMap)
        attributes([
            'Specification-Title':   'JavaFMLMod',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version': '1',
            'Implementation-Title':  'JavaFMLMod',
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': FORGE_VERSION
        ] as LinkedHashMap, 'net/minecraftforge/fml/javafmlmod/')
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

license {
    header = rootProject.file('LICENSE-header.txt')
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        from components.java
        artifactId = 'javafmllanguage'
        pom {
            name = project.name
            description = 'Language provider for Minecraft Forge that provides basic java mod functionality'
            url = 'https://github.com/MinecraftForge/MinecraftForge'
            PomUtils.setGitHubDetails(pom, 'MinecraftForge')
            license PomUtils.Licenses.LGPLv2_1
        }
    }
    
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}